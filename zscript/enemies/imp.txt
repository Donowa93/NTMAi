class NTM_Imp : NTM_Humanoid {
	Default {
		Health 60;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 9;
		PainChance 200;
		MaxDropOffHeight 80;
		SeeSound "imp/sight";
		ActiveSound "imp/active";
		PainSound "imp/pain";
		DeathSound "imp/death";
		NTM_Humanoid.BurnSound "imp/burn";
		HitObituary "$OB_IMPHIT";
		Obituary "$OB_IMP";
		Tag "$FN_IMP";
		Species "DoomImp";
		NTM_Enemy.TicsPerChase 3;
		NTM_Enemy.LeapDistance 360;
	}
	
	States {
		Spawn:
			NIMP A 10 A_Look;
			Loop;
		
		DukeSee:
			NIMP AABBCCDD 3 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NIMP AABBCCDD 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NIMP A 1 A_NTM_SWChase;
			NIMP AA 1 A_NTM_SWChase2;
			NIMP A 1 A_NTM_SWChase;
			NIMP AA 1 A_NTM_SWChase2;
			NIMP B 1 A_NTM_SWChase;
			NIMP BB 1 A_NTM_SWChase2;
			NIMP B 1 A_NTM_SWChase;
			NIMP BB 1 A_NTM_SWChase2;
			NIMP C 1 A_NTM_SWChase;
			NIMP CC 1 A_NTM_SWChase2;
			NIMP C 1 A_NTM_SWChase;
			NIMP CC 1 A_NTM_SWChase2;
			NIMP D 1 A_NTM_SWChase;
			NIMP DD 1 A_NTM_SWChase2;
			NIMP D 1 A_NTM_SWChase;
			NIMP DD 1 A_NTM_SWChase2;
			Loop;
		
		Melee:
			NIMP EF 6 A_FaceTarget;
			NIMP G 6 A_NTM_TroopMelee;
			NIMP G 0 A_JumpIfTargetInsideMeleeRange(1);
			Goto See;
			NIMP HI 6 A_FaceTarget;
			NIMP J 6 A_NTM_TroopMelee;
			Goto See;
			
		Missile:
			NIMP QR 7 A_FaceTarget;
			NIMP S 7 A_NTM_TroopAttack;
			Goto See;
		
		Pain:
			NIMP K 4 A_Pain;
			Goto See;
		
		Death:
			NIMP L 6 A_NTM_Death;
			NIMP M 6 A_NoBlocking;
			NIMP NO 6;
			NIMP P -1;
			Stop;
		
		Raise:
			NIMP PONML 6;
			Goto See;
		
		Leap:
			NIMP T 1 A_NTM_Leap(false);
			Loop;
	}
	
	action void A_NTM_TroopMelee() {
		A_Recoil(-2);
		A_CustomMeleeAttack(3 * Random(1, 8), "imp/melee");
	}
	
	action void A_NTM_TroopAttack() {
		A_CustomComboAttack("NTM_ImpBall", 32, 3 * Random(2, 8), "imp/melee");
	}
}

class NTM_ShadowImp : NTM_Imp {
	Default {
		RenderStyle "Stencil";
		Alpha 0.3;
		Obituary "%o was burnt by a Shadow Imp.";
		HitObituary "%o bumped into a Shadow Imp.";
		Tag "Shadow Imp";
		
		+Shadow
	}
}

class NTM_NightmareImp : NTM_ShadowImp {
	Default {
		Alpha 0.5;
		Obituary "%o was burnt by a Nightmare Imp.";
		HitObituary "%o was slashed by a Nightmare Imp.";
		Tag "Nightmare Imp";
	}
	
	States {
		Melee:
			NIMP EF 6 A_FaceTarget;
			NIMP G 6 A_NTM_NTroopMelee;
			NIMP G 0 A_JumpIfTargetInsideMeleeRange(1);
			Goto See;
			NIMP HI 6 A_FaceTarget;
			NIMP J 6 A_NTM_NTroopMelee;
			Goto See;
			
		Missile:
			NIMP QR 7 A_FaceTarget;
			NIMP S 7 A_CustomComboAttack("NTM_NightmareBall", 32, 10 * Random(2, 6), "imp/melee");
			Goto See;
	}
	
	action void A_NTM_NTroopMelee() {
		A_Recoil(-2);
		A_CustomMeleeAttack(10 * Random(1, 6), "imp/melee");
	}
}

class NTM_ImpBall : DoomImpBall {
	Default {
		Speed 12;
		FastSpeed 24;
		
		+ForceXYBillboard
		+Bright
	}
	
	States {
		Spawn:
			NIMB ABCD 2;
			Loop;
			
		Death:
			NEXP A 2 {
				scale *= 0.45;
				A_SetTranslation("NTM_Explosion");
			}
			
			NEXP BCDEFGHIJ 2;
			Stop;
	}
}

class NTM_NightmareBall : NTM_ImpBall {
	Default {
		Damage 5;
	}
	
	States {
		Spawn:
			NIMB EFGH 2;
			Loop;
		
		Death:
			NEXP A 2 {
				scale *= 0.45;
				A_SetTranslation("NTM_Explosion_Nightmare");
			}
			
			Goto Super::Death + 1;
	}
}